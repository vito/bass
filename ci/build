#!/usr/bin/env bass

(use (*dir*/../project))

(defn parse-args [args]
  (case args
    ; default 3rd arg (title) to tag
    [sha] [sha "linux" "amd64"]

    ; let it fail at pattern-match time
    _ args))

; runs a quick sanity check
(defn check-binary [binary image]
  (run (from image
         ($ $binary --version)))

  (if (succeeds? (from image
                   ($ ldd $binary)))
    (error "binary is not statically linked")
    :ok))

(defn main args
  (let [[sha os arch] (parse-args args)
        binary (project:build (project:checkout sha) os arch)]
    ; check that the binary works in a few common images
    (map (fn [image] (check-binary binary image))
         [(linux/ubuntu)
          (linux/alpine)])

    (emit binary *stdout*)))

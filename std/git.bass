(provide [ls-remote checkout]
  (def *git-image*
    (case (next *stdin* :none)
      :none (error "git image must be provided")
      image image))

  ; resolves a ref to a sha at the remote repo
  ;
  ; Does not cache. Used to resolve the ref at a point in time.
  ;
  ; => (use (.git (linux/alpine/git)))
  ;
  ; => (git:ls-remote "https://github.com/vito/bass" "main")
  (defn ls-remote [repo ref]
    (let [ls (from *git-image*
               (-> ($ git ls-remote $repo $ref)
                   (with-label :at (now 0))))]
      (first (next (read ls :unix-table)))))

  ; returns the repo checked out to the given ref
  ;
  ; The thunk for cloning the repo is labeled with the given ref. If the ref
  ; refers to a branch, you may want to resolve it to a sha first with
  ; (ls-remote) so that it's not cached forever.
  ;
  ; Submodules are always initialized.
  ;
  ; => (use (.git (linux/alpine/git)))
  ;
  ; => (git:checkout "https://github.com/vito/bass" "ea8cae6d4c871cb14448d7254843d86dbab8505f")
  (defn checkout [repo ref]
    (subpath
      (from *git-image*
        (-> ($ git clone $repo ./)
            (with-label :for ref))
        ($ git checkout $ref)
        ($ git submodule update --init --recursive))
      ./)))

; a root path for repos on github.com
;
; => (use (.git (linux/alpine/git)))
;
; => git:github/vito/bass/sha/ea8cae6d4c871cb14448d7254843d86dbab8505f/
;
; => git:github/vito/bass/ref/main/
(defn github [user]
  (fn [repo]
    (let [uri (str "https://github.com/" (path-name user) "/" (path-name repo))]
      (fn [route]
        (case route
          ./sha/
          (fn [sha] (checkout uri (path-name sha)))

          ./ref/
          (fn [ref]
            (checkout uri (ls-remote uri (path-name ref)))))))))

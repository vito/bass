; construct a list from a sequence of values
;
; => (list 1 2 3)
(def list (wrap (op values _ values)))

; prepend a sequence of values to a list given as the final argument
(def list*
  (wrap
    (op args _
      (do
        (def aux
          (wrap
            (op ((head & tail)) _
              (if (empty? tail)
                head
                (cons head (aux tail))))))
        (aux args)))))

; return the first value in a pair
(def first (wrap (op [(f & _)] _ f)))

; return the second value in a pair, i.e. the rest of a linked list
(def rest (wrap (op [(_ & r)] _ r)))

; return the length of the given list
(def length
  (wrap
    (op [xs] _
        (if (empty? xs)
          0
          (+ 1 (length (rest xs)))))))

; construct an operative
;
; An operative is a combiner that is called with unevaluated arguments and the
; caller's dynamic scope.
;
; Operatives are used to define new syntactic constructs.
^:indent
(def op
  ((wrap
     (op (op) _
         (op [formals eformal & body] scope
           (eval [op formals eformal
                  (if (> (length body) 1)
                    (cons do body)
                    (first body))]
                 scope))))
   op))

; construct an operative and bind it to a symbol
;
; Returns the bound symbol. Write a comment before (defop) to provide
; documentation.
^:indent
(def defop
  (op [name formals eformal & body] scope
    (eval [def name [op formals eformal & body]] scope)))

; construct a function
;
; Functions are applicative combiners which evaluate their arguments before
; passing them to an underlying operative.
^:indent
(defop fn [formals & body] scope
  (wrap (eval [op formals _ & body] scope)))

; construct a function and bind it to a symbol
;
; Returns the bound symbol. Write a comment before (defn) to provide
; documentation.
^:indent
(defop defn [name formals & body] scope
  (eval [def name [fn formals & body]] scope))

; return the second member of a linked list
(defn second [(_ x & _)]   x)

; return third member of a linked list
(defn third  [(_ _ x & _)] x)

; return the current scope
(defop get-current-scope _ scope scope)

; returns the unevaluated form
(defop quote [form] _ form)

; returns a list containing the result of applying f to each member of xs
(def map
  (fn [f xs]
    (if (empty? xs)
      ()
      (cons (f (first xs))
            (map f (rest xs))))))

; calls a function with alternating pairs in a flat list (i.e. with pairs ungrouped)
;
; Takes 2-arity function and a flat pair sequence. Walks the sequence and calls
; f with 2 values at a time.
;
; Raises an error if the list has uneven length.
;
; => (map-pairs cons [:a 1 :b 2 :c 3])
(defn map-pairs [f ps]
  (defn map-pairs-acc [f a ps]
    (if (empty? ps)
      (if (null? a)
        []
        (error "syntax: uneven pairs"))
      (if (null? a)
        (map-pairs-acc f (first ps) (rest ps))
        (cons (f a (first ps))
              (map-pairs-acc f null (rest ps))))))

  (map-pairs-acc f null ps))

; binds values in a child scope
;
; Takes a flat pair sequence alternating bindings and their values. Binds
; and evaluates each value in sequence, allowing later bindings to refer to
; earlier bindings.
;
; Returns the result of evaluating the body in the child scope.
^:indent
(defop let (bindings & body) scope
  (def child (make-scope scope))

  (map-pairs
    (fn [binding val]
      (eval [def binding val] child))
    bindings)

  (eval [do & body] child))

; binds symbols in the current scope to their values from the source
; scope
(defop import [source & symbols] scope
  (let [inner (eval source scope)]
    (map
      (fn [sym]
        (let [val-thunk [(unwrap eval) sym inner]]
          (eval [def sym val-thunk] scope)
          sym))
      symbols)))

; provide bindings to the current scope from a nested scope
;
; Allows for modularity in code, selectively providing bindings while
; encapsulating bindings that they use.
^:indent
(defop provide [symbols & body] scope
  (let [inner (make-scope scope)]
    (eval [do & body] inner)
    (eval [import inner & symbols] scope)))

(provide [cond]
  (defn cond-pairs [f a ps]
    (if (empty? ps)
      (if (null? a)
        null
        uneven-pairs) ; TODO: proper error
      (if (null? a)
        (cond-pairs f (first ps) (rest ps))
        (do
          (def (ok & res) (f a (first ps)))
          (if ok
            res
            (cond-pairs f null (rest ps)))))))

  ; if-then-else with many clauses
  ;
  ; Takes a flat pair sequence alternating tests to evaluate and an
  ; expression to evaluate if the test returns a truthy value.
  ;
  ; Returns the result of the evaluated branch, or null if no tests were true.
  ;
  ; By convention, :else is used as the final catch-all test, though any
  ; truthy value works.
  ^:indent
  (defop cond clauses scope
    (cond-pairs
      (fn [test body]
        (if (eval test scope)
          (cons true (eval body scope))
          (cons false null)))
      null
      clauses)))

; lazily evaluated boolean 'or'
(defop or conds scope
  (cond
    (empty? conds)
      false

    (= 1 (length conds))
      (eval (first conds) scope)

    :else
      (let [(x & xs) conds
            xv (eval x scope)]
        (if xv
          xv
          (eval [or & xs] scope)))))

; lazily evaluated boolean 'and'
(defop and conds scope
  (cond
    (empty? conds)
      true

    (= 1 (length conds))
      (eval (first conds) scope)

    :else
      (let [(x & xs) conds
            xv (eval x scope)]
        (if xv
          (eval [and & xs] scope)
          xv))))

; call an applicative's underlying operative with a list of arguments
;
; A scope may be provided as the third argument. If not specified, the
; operative will be called in a new empty scope.
;
; Used to call an applicative with pre-evaluated arguments, skipping the normal
; evaluation the applicative would perform prior to calling the underlying
; operative.
(defn apply [appv arg & opt]
  (eval (cons (unwrap appv) arg)
        (if (empty? opt)
          (make-scope)
          (first opt))))

(provide (->)
  ; threads a value through forms
  ;
  ; Calls the first form with the given value, passing the result to the second
  ; form, and so on, returning the final value.
  ;
  ; A form may be a list or a single value. For a list, the value is inserted
  ; before the second value, becoming the first argument. For a single value,
  ; it is wrapped in a list with the value as the single argument.
  (defop -> [x f & fs] scope
    (let [v (if (pair? f)
              (let [(f' & as) f]
                (eval [f' x & as] scope))
              (eval [f x] scope))]
      (if (empty? fs)
        v
        (eval [-> v & fs] scope)))))

(provide (case)
  (defn case-branches [scope val branches]
    (if (empty? branches)
      (errorf "no matching case branch: %s" val)
      (let [[pattern expr & rest] branches
            child (make-scope scope)]
        (if (bind child pattern val)
          (eval expr child)
          (case-branches scope val rest)))))

  ; evaluates the branch that successfully binds the given value
  ^:indent
  (defop case [v & bs] scope
    (case-branches scope (eval v scope) bs)))

; identity function; returns its argument
(defn id [x] x)
